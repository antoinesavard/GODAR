from glob import glob
import os

# This file sets up the compilation environment for Godar and its libraries.

# ----------------------------------
#         SCONS Variables
# ----------------------------------

# LIBPATH : The path searched for libraries.
# FORTRANMODDIR : The path where .mod files are stored, ie compiled modules.
# FORTRANMODDIRPREFIX : The compiler prefix that prefixes FORTRANMODDIR (-J for gfortran)
# F90PATH : The path searched for include files.
# F90FLAGS : Flags for the F90 compiler.


# ----------------------------------
#              Notes
# ----------------------------------
#
# Flags
# -----
#   -fbackslash : Allow escape characters \n, \t, etc in strings.
#                 In gcc-4.3, -fno-backslash is the default.
#   -Wall : Print all warnings.

# ----------------------------------
#  Standard Compilation Environment
# ----------------------------------

include = Dir("#/include")
libs = Dir("#/libs")
FC = F90 = "mpifort"

env = Environment(
    ENV=os.environ,
    LIBPATH=[libs],
    FORTRANMODDIR=[include],
    FORTRANMODDIRPREFIX="-J",
    F90PATH=[include],
    F90FLAGS=[
        "-Wall",
        "-Wno-tabs",
        "-fPIC",
        "-fbackslash",
        "-ffast-math",
        "-mcmodel=medium",
        "-fopenmp",
    ],
    LINKFLAGS=[
        "-fopenmp",
        "-lnetcdff",
    ],
    FORTRAN=FC,
    F90=FC,
)

# ---------------------------------------
#           Coretran Librairy
# ---------------------------------------

# you need to change these two paths 
# should be done automatically
env.Append(LIBPATH="/path/to/coretran/lib")
env.Append(F90PATH="/path/to/coretran/include")

# ---------------------------------------
#           NetCDF and hdf5 Librairies
# ---------------------------------------

# you need to change these paths for netcdf
env.Append(LIBPATH='/path/to/netcdf/lib')
env.Append(F90PATH='/path/to/netcdf/include')

# and for hdf5
# they probably are at the same place
env.Append(LIBPATH='/path/to/hdf5/lib')
env.Append(F90PATH='/path/to/hdf5/include')


# Command line options to modify the build environment.
# Use scons-3 debug=1 to compile with debugging flags.


DEBUG = ARGUMENTS.get("debug", 0)
EXE = ARGUMENTS.get("exe", "godar")

if int(DEBUG) == 0:
    env.Append(F90FLAGS="-O3")

if int(DEBUG):
    env.Append(F90FLAGS="-g")
    env.Append(F90FLAGS="-Og")
    env.Append(F90FLAGS="-static")
    env.Append(F90FLAGS="-fbacktrace")
    env.Append(F90FLAGS="-fcheck=all")
    env.Append(LINKFLAGS="-fbacktrace")

# ----------------------------------------
#             Create Executables
# ----------------------------------------

# Export the compilation environment so SConscript files in subdirectories can access it.

Export("env")
Export("EXE")

# The datetime module
datetime = SConscript("datetime/SConscript", variant_dir="build/datetime")

# The GoDAR library and executable
godar = SConscript("src/SConscript", variant_dir="build/godar")
